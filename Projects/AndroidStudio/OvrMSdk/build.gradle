// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
        mavenLocal()
    }
    dependencies {
//        classpath 'com.android.tools.build:gradle-experimental:0.8.3'
        classpath 'com.android.tools.build:gradle:2.2.3'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }

    ext{
        commonCPPFlags = ["-DGLES2_0"]
    }

}

allprojects {
    repositories {
        jcenter()
        mavenCentral()
    }
}


task clean(type: Delete) {
    delete rootProject.buildDir
}


// Recursive clean subprojects - maybe overkill - rebuild takes a couple of minutes
subprojects {
    rootProject.clean {
        dependsOn tasks.matching { it.name == "clean" }
    }
}

// The dependencies for NDK builds live inside the .aar files so they need to
// be extracted before NDK targets can build.
task extractAudioSo(type: Copy) {
    from zipTree("${project.rootDir}/gvr_libraries/sdk-audio-1.30.0.aar")
    into "${project.rootDir}/gvr_libraries/"
    include "jni/**/libgvr_audio.so"
}

task extractGvrSo(type: Copy) {
    from zipTree("${project.rootDir}/gvr_libraries/sdk-base-1.30.0.aar")
    into "${project.rootDir}/gvr_libraries/"
    include "jni/**/libgvr.so"
}

task extractNdk { }
extractNdk.dependsOn extractAudioSo
extractNdk.dependsOn extractGvrSo

task deleteNdk(type: Delete) {
    delete "${project.rootDir}/gvr_libraries/jni"
}
clean.dependsOn(deleteNdk)