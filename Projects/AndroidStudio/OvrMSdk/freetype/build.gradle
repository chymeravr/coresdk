apply plugin: 'com.android.library'

def freetypeModule = "${rootProject.rootDir}/freetype"
def distributionDir = "${rootProject.rootDir}/distribution"

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"

        externalNativeBuild {
            cmake {
                // when using arguments, the latest (cmake version 3.6+, android studio
                // beta2+) must be used to compile

                // I have to use gcc to compile, clang compiled code generate error
                // at run time for any std::queue etc.
                targets 'freetype'
                arguments = ['-DANDROID_STL=gnustl_shared', '-DANDROID_NATIVE_API_LEVEL=android-23']

                cFlags = ["-DANDROID_NDK", "-DFT2_BUILD_LIBRARY=1"]

                if(project.build4daydream) {
                    // build for all daydream architectures concurrently
                    if (project.buildAllDaydreamArch) {
                        // build architectures for native android release lib
                        abiFilters = ['armeabi-v7a', 'arm64-v8a', 'x86']
                    }
                    else if (project.build4Unity) {
                        // unity does not support arm64-v8a
                        abiFilters = ['armeabi-v7a', 'x86']
                    } else {
                        // for pixel only - we dev on a 64 bit arch
                        abiFilters = ['arm64-v8a']
                    }
                }

                // for SAMSUNG GEARVR
                if(project.build4gearvr){
                    abiFilters = ['armeabi-v7a']
                }
            }
        }

    }
    buildTypes {
        debug {
            minifyEnabled false
            jniDebuggable true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        cmake {
            path 'CMakeLists.txt'
            //path '../../../../freetype/CMakeLists.txt'
        }
    }
}

def copyNativeLib(source, target, buildType, arch) {
    copy {
        from "${source}/build/intermediates/cmake/${buildType}/obj/${arch}/libfreetype.so"
        into "${target}/freetype/${arch}/"
    }
}

task distributeDebugLib << {
    // trigger build library

    if(project.build4daydream) {

        if(project.buildAllDaydreamArch){
            copyNativeLib(freetypeModule, distributionDir, "debug", "armeabi-v7a")
            copyNativeLib(freetypeModule, distributionDir, "debug", "x86")
            copyNativeLib(freetypeModule, distributionDir, "debug", "arm64-v8a")
        }
        else if (project.build4Unity) {
            copyNativeLib(freetypeModule, distributionDir, "debug", "armeabi-v7a")
            copyNativeLib(freetypeModule, distributionDir, "debug", "x86")
        } else {
            copyNativeLib(freetypeModule, distributionDir, "debug", "arm64-v8a")
        }

    }

    if(project.build4gearvr) {
        copyNativeLib(freetypeModule, distributionDir, "debug", "armeabi-v7a")
    }
}

task distributeReleaseLib << {
    // trigger build library

    if(project.build4daydream) {

        if(project.buildAllDaydreamArch){
            copyNativeLib(freetypeModule, distributionDir, "release", "armeabi-v7a")
            copyNativeLib(freetypeModule, distributionDir, "release", "x86")
            copyNativeLib(freetypeModule, distributionDir, "release", "arm64-v8a")
        }
        else if(project.build4Unity){
            copyNativeLib(freetypeModule, distributionDir, "release", "armeabi-v7a")
            copyNativeLib(freetypeModule, distributionDir, "release", "x86")
        }
        else{
            copyNativeLib(freetypeModule, distributionDir, "release", "arm64-v8a")
        }
    }

    if(project.build4gearvr) {
        copyNativeLib(freetypeModule, distributionDir, "release", "armeabi-v7a")
    }
}

build.finalizedBy(distributeReleaseLib)